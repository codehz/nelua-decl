##[[
-- Disables "note: the ABI of passing union with 'long double' has changed"
if ccinfo.is_gcc then
  cflags '-fcompare-debug-second'
end
]]

require 'mir'
require 'string'
require 'hashmap'
require 'io'
require 'os'
require 'C.stdio'
require 'C.stdlib'
require 'C.string'


local StringReader = @record{
  str: string,
  pos: isize
}

function StringReader:getc(): cint
  if self.pos <= #self.str then
    local c: byte = self.str[self.pos]
    self.pos = self.pos + 1
    return c
  end
  return C.EOF
end

local function compile(ctx: MIR_context_t, source: string, name: string): boolean
  local reader: StringReader = {source, 1}
  local opts: c2mir_options = {message_file = C.stderr}
  if c2mir_compile(ctx, &opts, (@function(pointer):cint)(StringReader.getc), &reader, name, nilptr) == 0 then
    return false
  end
  c2mir_finish(ctx)
  return true
end

local import_symbols: hashmap(string, pointer)

##[[
local function fill_import_symbols()
  local iterators = require 'nelua.utils.iterators'
  local ignored = {tmpnam = true, gets = true}
  for k,v in iterators.ospairs(C.value.metafields) do
    if not ignored[k] then
      if v.type.is_function then ]]
        import_symbols[#[k]#] = (@pointer)(#[v]#)
##[[  elseif v.type.is_pointer then ]]
        local #|k..'_storage'|#: pointer = (@pointer)(#[v]#)
        import_symbols[#[k]#] = &#|k..'_storage'|#
##[[  end
    end
  end
end

fill_import_symbols()
]]

local function import_resolver(name: cstring): pointer
  local paddr = import_symbols:peek(name)
  if paddr then
    return $paddr
  end
  return nilptr
end

local function load_func(ctx: MIR_context_t, name: string): MIR_item_t
  local list = MIR_get_module_list(ctx)
  local module = DLIST_MIR_module_t_head(MIR_get_module_list(ctx))
  local main_func = nilptr
  while module do
    local item = DLIST_MIR_item_t_head(module.items)
    while item do
      if item.item_type == MIR_func_item and item.u.func.name == name then
        main_func = item
      end
      item = DLIST_MIR_item_t_next(item)
    end
    MIR_load_module(ctx, module)
    module = DLIST_MIR_module_t_next(module)
  end
  return main_func
end

local function run_c(code: string, name: string): integer
  local ctx: MIR_context_t = MIR_init()
  c2mir_init(ctx)
  compile(ctx, code, '<input>')
  local main_func = load_func(ctx, name)
  MIR_link(ctx, MIR_set_interp_interface, import_resolver)
  local res: MIR_val_t
  local argc: cint = 0
  local argv: *cstring = nilptr
  local env: pointer = nilptr
  MIR_interp(ctx, main_func, &res, 3, (@MIR_val_t){i=argc}, (@MIR_val_t){a=argv}, (@MIR_val_t){a=env})
  MIR_finish(ctx)
  return res.i
end

local function compile_nelua_to_c(code: string): string
  local filename = os.tmpname()
  local file <close> = io.open(filename, 'w')
  file:write(code)
  local out <close> = io.popen(string.format("nelua --print-code %s", filename))
  return out:read('a')
end

local nelua_code = [[
print 'hello'
return 10
]]

local c_code = compile_nelua_to_c(nelua_code)
local res = run_c(c_code, 'main')
assert(res == 10)
print("mir OK!")
